function(proDeps)
  set(options XP_MODULE)
  set(oneValueArgs PKG BASE BRANCH DESC DIST_DIR LICENSE REPO TAG UPSTREAM VER WEB SHA256_Linux SHA256_win64 URL_Linux URL_win64 SHA256_utres)
  set(multiValueArgs DEPS EXE_DEPS)
  cmake_parse_arguments(P "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  string(APPEND dot "  \"${P_PKG}\" [shape=diamond]\;\n")
  if(DEFINED P_DEPS OR DEFINED P_EXE_DEPS)
    foreach(dep ${P_DEPS} ${P_EXE_DEPS})
      string(APPEND dot "  \"${P_PKG}\" -> \"${dep}\"\;\n")
    endforeach()
  endif()
  set(dot "${dot}" PARENT_SCOPE)
endfunction()
string(JOIN "\n" dot
  "digraph GG {"
  "  node [fontsize=12]\;"
  ""
  )
include(${CMAKE_CURRENT_LIST_DIR}/pros.cmake)
# https://stackoverflow.com/q/9298278/
get_cmake_property(vars VARIABLES)
list(SORT vars)
foreach(var ${vars})
  if(var MATCHES "^xp_")
    string(REGEX REPLACE "^xp_" "" pkg ${var})
    proDeps(PKG ${pkg} ${${var}})
  endif()
endforeach()
string(APPEND dot "}\n")
set(binDot ${CMAKE_BINARY_DIR}/deps.dot)
set(binPng ${CMAKE_BINARY_DIR}/deps.png)
set(srcDot ${CMAKE_CURRENT_LIST_DIR}/deps.dot)
set(srcPng ${CMAKE_CURRENT_LIST_DIR}/deps.png)
file(WRITE ${binDot} ${dot})
find_program(XP_DOT_PATH "dot")
mark_as_advanced(XP_DOT_PATH)
if(XP_DOT_PATH AND EXISTS ${binDot})
  execute_process(COMMAND dot -Tpng -o${binPng} ${binDot})
endif()
if(EXISTS ${binDot})
  execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different ${binDot} ${srcDot})
  file(REMOVE ${binDot})
endif()
if(EXISTS ${binPng})
  execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different ${binPng} ${srcPng})
  file(REMOVE ${binPng})
endif()
