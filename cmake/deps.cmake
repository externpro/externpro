function(proDeps)
  set(oneValueArgs PKG BASE BRANCH DESC DIST_DIR LICENSE MANIFEST_FILE MANIFEST_SHA256 REPO TAG UPSTREAM VER WEB XPBLD)
  list(APPEND oneValueArgs SHA256_Darwin-arm64 SHA256_Linux SHA256_Linux-arm64 SHA256_win64)
  list(APPEND oneValueArgs URL_Darwin-arm64 URL_Linux URL_Linux-arm64 URL_win64 SHA256_utres)
  set(multiValueArgs DEPS EXE_DEPS)
  cmake_parse_arguments(P "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  # manifest
  if(DEFINED P_MANIFEST_SHA256 AND DEFINED P_REPO AND DEFINED P_TAG)
    string(REGEX REPLACE ".*/" "" PKG_NAME ${P_REPO})
    set(dst ${XPRO_DIR}/xpd/manifests/${PKG_NAME}-${P_TAG}.manifest.cmake)
    if(NOT EXISTS ${dst})
      ipDownload(https://${P_REPO}/releases/download/${P_TAG}/${PKG_NAME}-${P_TAG}.manifest.cmake ${P_MANIFEST_SHA256} ${dst})
    endif()
    include(${dst})
  endif()
  if(DEFINED P_MANIFEST_FILE AND EXISTS ${P_MANIFEST_FILE})
    include(${P_MANIFEST_FILE})
  endif()
  # temporarily copy manifest variables to P_ variables
  # (until all projects are updated and generate manifest)
  if(DEFINED XP_MANIFEST_BASE)
    set(P_BASE ${XP_MANIFEST_BASE})
  endif()
  if(DEFINED XP_MANIFEST_DESC)
    set(P_DESC ${XP_MANIFEST_DESC})
  endif()
  if(DEFINED XP_MANIFEST_LICENSE)
    set(P_LICENSE ${XP_MANIFEST_LICENSE})
  endif()
  if(DEFINED XP_MANIFEST_UPSTREAM)
    set(P_UPSTREAM ${XP_MANIFEST_UPSTREAM})
  endif()
  if(DEFINED XP_MANIFEST_WEB)
    set(P_WEB ${XP_MANIFEST_WEB})
  endif()
  if(DEFINED XP_MANIFEST_XPDIFF)
    set(P_XPBLD ${XP_MANIFEST_XPDIFF})
  endif()
  if(DEFINED XP_MANIFEST_DEPS)
    set(P_DEPS ${XP_MANIFEST_DEPS})
  endif()
  if(DEFINED XP_MANIFEST_PVT_DEPS)
    set(P_EXE_DEPS ${XP_MANIFEST_PVT_DEPS})
  endif()
  # dot
  if(DEFINED P_DEPS OR DEFINED P_EXE_DEPS)
    foreach(dep ${P_DEPS} ${P_EXE_DEPS})
      string(APPEND dot "  \"${P_PKG}\" -> \"${dep}\"\;\n")
    endforeach()
  endif()
  set(dot "${dot}" PARENT_SCOPE)
  # README project
  if(DEFINED P_WEB)
    string(APPEND rme "|<a id='${P_PKG}' />[${P_PKG}](${P_WEB})")
  else()
    string(APPEND rme "|<a id='${P_PKG}' />${P_PKG}")
  endif()
  # README license
  if(DEFINED P_LICENSE)
    string(APPEND rme "|${P_LICENSE}")
  else()
    string(APPEND rme "| ")
  endif()
  # README description [dependencies]
  if(DEFINED P_DESC)
    string(APPEND rme "|${P_DESC}")
    if(DEFINED P_DEPS)
      list(JOIN P_DEPS ", " deps)
      string(APPEND rme " [deps: _${deps}_]")
    endif()
    if(DEFINED P_EXE_DEPS)
      list(JOIN P_EXE_DEPS ", " deps)
      string(APPEND rme " [pvt deps: _${deps}_]")
    endif()
  else()
    string(APPEND rme "| ")
  endif()
  # README version
  if(DEFINED P_VER)
    string(APPEND rme "|${P_VER}")
  elseif(DEFINED P_TAG)
    string(APPEND rme "|[${P_TAG}](https://${P_REPO}/releases/tag/${P_TAG} 'release')")
  else()
    string(APPEND rme "| ")
  endif()
  # README source
  if(DEFINED P_REPO)
    string(APPEND rme "|[repo](https://${P_REPO} '${P_REPO}')")
    if(DEFINED P_UPSTREAM)
      string(APPEND rme " [upstream](https://${P_UPSTREAM} '${P_UPSTREAM}')")
    endif()
  elseif(DEFINED P_UPSTREAM)
    string(APPEND rme "|[upstream](https://${P_UPSTREAM} '${P_UPSTREAM}')")
  else()
    string(APPEND rme "| ")
  endif()
  # README diff
  if(DEFINED P_REPO AND DEFINED P_BASE AND DEFINED P_BRANCH)
    string(APPEND rme "|[diff](https://${P_REPO}/compare/${P_BASE}...${P_BRANCH} '${P_REPO}/compare/${P_BASE}...${P_BRANCH}')")
  elseif(DEFINED P_REPO AND DEFINED P_BASE AND DEFINED P_TAG)
    string(APPEND rme "|[diff](https://${P_REPO}/compare/${P_BASE}...${P_TAG} '${P_REPO}/compare/${P_BASE}...${P_TAG}')")
  else()
    string(APPEND rme "| ")
  endif()
  # append XPBLD to 'diff' cell
  if(DEFINED P_XPBLD)
    string(APPEND rme " [${P_XPBLD}]")
  endif()
  set(rme "${rme}|\n" PARENT_SCOPE)
endfunction()
string(JOIN "\n" dot
  "digraph GG {"
  "  node [fontsize=12]\;"
  ""
  )
string(JOIN "\n" rme
  "# projects"
  "this README.md and the [deps.dot](deps.dot) file"
  "are generated by [deps.cmake](deps.cmake) from"
  "the contents of [pros.cmake](pros.cmake)"
  "and downloaded project manifest files"
  "<a id='DIFF_TABLE'>&nbsp\;</a>"
  "|diff  |description|"
  "|------|-----------|"
  "|patch |diff modifies/patches existing cmake|"
  "|intro |diff introduces cmake|"
  "|auto  |diff adds cmake to replace autotools/configure/make|"
  "|native|diff adds cmake but uses existing build system|"
  "|bin   |diff adds cmake to repackage binaries built elsewhere|"
  "|fetch |diff adds cmake and utilizes FetchContent|"
  ""
  "[SPDX License List](https://spdx.org/licenses/ '')"
  "<a id='PROJECT_TABLE'>&nbsp\;</a>"
  "|project|license|description [dependencies]|version|source|diff|"
  "|-------|-------|--------------------------|-------|------|----|"
  ""
  )
include(${CMAKE_CURRENT_LIST_DIR}/pros.cmake)
# https://stackoverflow.com/q/9298278/
get_cmake_property(vars VARIABLES)
list(SORT vars)
foreach(var ${vars})
  if(var MATCHES "^xp_")
    string(REGEX REPLACE "^xp_" "" pkg ${var})
    proDeps(PKG ${pkg} ${${var}})
  endif()
endforeach()
string(APPEND dot "}\n")
string(APPEND rme "\n![deps](https://g.gravizo.com/g?url=https%3A%2F%2Fraw.githubusercontent.com%2Fexternpro%2Fexternpro%2Fmain%2Fcmake%2Fdeps.dot 'dependencies')\n")
string(APPEND rme "\n[How-to: modify a project to build with externpro](pros.md)\n")
set(binDot ${CMAKE_BINARY_DIR}/deps.dot)
set(binPng ${CMAKE_BINARY_DIR}/deps.png)
set(binRme ${CMAKE_BINARY_DIR}/README.md)
set(srcDot ${CMAKE_CURRENT_LIST_DIR}/deps.dot)
set(srcPng ${CMAKE_CURRENT_LIST_DIR}/deps.png)
set(srcRme ${CMAKE_CURRENT_LIST_DIR}/README.md)
file(WRITE ${binDot} ${dot})
file(WRITE ${binRme} ${rme})
find_program(XP_DOT_PATH "dot")
mark_as_advanced(XP_DOT_PATH)
if(XP_DOT_PATH AND EXISTS ${binDot})
  execute_process(COMMAND dot -Tpng -o${binPng} ${binDot})
endif()
if(EXISTS ${binDot} AND NOT CMAKE_SYSTEM_NAME STREQUAL "Windows")
  execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different ${binDot} ${srcDot})
  file(REMOVE ${binDot})
endif()
if(EXISTS ${binRme} AND NOT CMAKE_SYSTEM_NAME STREQUAL "Windows")
  execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different ${binRme} ${srcRme})
  file(REMOVE ${binRme})
endif()
